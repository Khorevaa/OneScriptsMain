
Процедура СформироватьОтчетыПоКонфигурациям()

	СписокОбъектовОтчета = ОбъектыОтчета();
	Для Каждого ОбъектОтчета Из СписокОбъектовОтчета Цикл
		ВремяИзмененияФайла = ВремяИзмененияФайлаОтчета();
		ВыгрузитьОтчетПоХранилищу(ОбъектОтчета);
		Пока ВремяИзмененияФайлаОтчета() <= ВремяИзмененияФайла Цикл
			// Задержка времени для формирования отчета
		КонецЦикла;
		ДанныеПоИзменениямВХранилище();
	КонецЦикла;

КонецПроцедуры

Процедура СформироватьДанныеПоОтчету(ОтчетПоИзменениям, МассивИзменений)

	СоответствиеДанныхИзменения = Новый Соответствие;
	СтрокаОтчета = ОтчетПоИзменениям.ПрочитатьСтроку();
	Ключ = "";
	Значение = "";
	Пока СтрокаОтчета <> Неопределено Цикл
		Если СтрНайти(СтрокаОтчета, "#") > 0 Тогда
			СтрокаОтчета = ОтчетПоИзменениям.ПрочитатьСтроку();
			Прервать;
		КонецЕсли;
		СтрокаОтчета = ОтчетПоИзменениям.ПрочитатьСтроку();
	КонецЦикла;
	УбитьЦикл = Ложь;
	Пока СтрокаОтчета <> Неопределено Цикл
		Если СтрНайти(СтрокаОтчета, "Версия") > 0 Тогда
			МассивИзменений.Добавить(СоответствиеДанныхИзменения);
			Прервать;
		КонецЕсли;
		Если СтрНайти(СтрокаОтчета, "#") > 0 Тогда
			Если ПустаяСтрока(Ключ) Тогда
				Ключ = ДанныеИзСтрокиОтчета(СтрокаОтчета);
			Иначе
				Значение = ДанныеИзСтрокиОтчета(СтрокаОтчета);
			КонецЕсли;
			Если Не ПустаяСтрока(Ключ)
				И Не ПустаяСтрока(Значение) Тогда
				СоответствиеДанныхИзменения.Вставить(Ключ, Значение);
				Если Ключ = "Комментарий" Тогда
					МассивИзменений.Добавить(СоответствиеДанныхИзменения);
					УбитьЦикл = Истина;
				КонецЕсли;
				Ключ = "";
				Значение = "";
			КонецЕсли;
		КонецЕсли;
		Если УбитьЦикл Тогда
			Прервать;
		КонецЕсли;
		СтрокаОтчета = ОтчетПоИзменениям.ПрочитатьСтроку();
	КонецЦикла;

КонецПроцедуры

Функция ДанныеИзСтрокиОтчета(СтрокаОтчета)

	ПодстрокаДанных = Сред(СтрокаОтчета, 6);
	ПодстрокаДанных = СтрЗаменить(ПодстрокаДанных, ":", "");
	ПодстрокаДанных = СтрЗаменить(ПодстрокаДанных, "}", "");
	ПодстрокаДанных = СтрЗаменить(ПодстрокаДанных, """", "");

	Возврат ПодстрокаДанных;
	
КонецФункции

Функция ДанныеПоИзменениямВХранилище()

	ОтчетПоИзменениям = Новый ЧтениеТекста(ПолныйПутьКФайлуОтчета(), "UTF-8");
	МассивИзменений = Новый Массив;
	СтрокаОтчета = ОтчетПоИзменениям.ПрочитатьСтроку();
	Пока СтрокаОтчета <> Неопределено Цикл
		Если СтрНайти(СтрокаОтчета, "Версия") > 0 Тогда
			СформироватьДанныеПоОтчету(ОтчетПоИзменениям, МассивИзменений);
			СтрокаОтчета = ОтчетПоИзменениям.ПрочитатьСтроку();
		Иначе
			СтрокаОтчета = ОтчетПоИзменениям.ПрочитатьСтроку();
		КонецЕсли;
	КонецЦикла;
	ОтчетПоИзменениям.Закрыть();
	СформироватьОтчетПоОбъекту(МассивИзменений);

КонецФункции

Процедура СформироватьОтчетПоОбъекту(МассивИзменений)

	ФайлОтчет = Новый ЗаписьТекста (ПутьКСводномуОтчетуПоВнесеннымИзменениям());
	Для Каждого ЭлементМассива Из МассивИзменений Цикл
		Если СтрДлина(ЭлементМассива["Комментарий"]) < 20 Тогда
		Для Каждого ЭлементСоответсвия Из ЭлементМассива Цикл
			ФайлОтчет.ЗаписатьСтроку(ЭлементСоответсвия.Ключ + " - " + ЭлементСоответсвия.Значение);
		КонецЦикла;
		ФайлОтчет.ЗаписатьСтроку("++++++++++++++++++++++++++++++++++");
		КонецЕсли;
	КонецЦикла;
	ФайлОтчет.Закрыть();

КонецПроцедуры

Процедура ВыгрузитьОтчетПоХранилищу(ОбъектОтчета)

	СтрокаКонфигурации = СтрокаЗапускаКонфигурации(ОбъектОтчета);
	СтрокаХранилища = СтрокаПодключенияКХранилищу(ОбъектОтчета);
	СтрокаОтчета = СтрокаФормированияОтчета(ОбъектОтчета);
	Сообщить(СтрокаКонфигурации + СтрокаХранилища + СтрокаОтчета);
	ЗапуститьПриложение(СтрокаКонфигурации + СтрокаХранилища + СтрокаОтчета);

КонецПроцедуры

Функция СтрокаЗапускаКонфигурации(ОбъектОтчета)

	СтрокаЗапуска = """" + ПолныйПутьКПриложению1С() + """ DESIGNER /F""" + ОбъектОтчета.ПутьККонфигурации + """";
	Если Не ПустаяСтрока(ОбъектОтчета.ПользовательКонфигурации) Тогда
		СтрокаЗапуска = СтрокаЗапуска + " /N" + """" + ОбъектОтчета.ПользовательКонфигурации + """";
		Если Не ПустаяСтрока(ОбъектОтчета.ПарольКонфигурации) Тогда
			СтрокаЗапуска = СтрокаЗапуска + " /P" + """" + ОбъектОтчета.ПарольКонфигурации + """";
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрокаЗапуска;

КонецФункции

Функция СтрокаПодключенияКХранилищу(ОбъектОтчета)

	СтрокаХранилища = " /ConfigurationRepositoryF " + """" + ОбъектОтчета.ПутьКХранилищу + """";
	Если Не ПустаяСтрока(ОбъектОтчета.ПользовательХранилища) Тогда
		СтрокаХранилища = СтрокаХранилища +  "/ConfigurationRepositoryN """ + ОбъектОтчета.ПользовательХранилища + """";
		Если Не ПустаяСтрока(ОбъектОтчета.ПарольХранилища) Тогда
			СтрокаХранилища = СтрокаХранилища +  "/ConfigurationRepositoryP """ + ОбъектОтчета.ПарольХранилища + """";
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрокаХранилища;
	
КонецФункции

Функция СтрокаФормированияОтчета(ОбъектОтчета)

	СтрокаОтчета = " /ConfigurationRepositoryReport """ + ПолныйПутьКФайлуОтчета() + """ -NBegin " + ОбъектОтчета.НомерПоследнейВерсии;
	
	Возврат СтрокаОтчета;

КонецФункции

Функция ПолныйПутьКФайлуОтчета()

	Возврат "C:\ByComment.mxl";

КонецФункции

Функция ПолныйПутьКПриложению1С()

	Возврат "C:\Program Files (x86)\1cv8\common\1cestart.exe";

КонецФункции

Функция ОбъектыОтчета()

	СписокОбъектовОтчета = Новый Массив;
	ОбъектОтчета = Новый Структура;
	ОбъектОтчета.Вставить("ПутьККонфигурации", "C:\1CBases\CI\LotsOperations");
	ОбъектОтчета.Вставить("ПользовательКонфигурации", "");
	ОбъектОтчета.Вставить("ПарольКонфигурации", "");
	ОбъектОтчета.Вставить("ПутьКХранилищу", "C:\1CBases\CI\LotsOperations1CGit");
	ОбъектОтчета.Вставить("ПользовательХранилища", "Администратор");
	ОбъектОтчета.Вставить("ПарольХранилища", "");
	ОбъектОтчета.Вставить("НомерПоследнейВерсии", "1");
	СписокОбъектовОтчета.Добавить(ОбъектОтчета);

	Возврат СписокОбъектовОтчета;
	
КонецФункции

Функция ВремяИзмененияФайлаОтчета()

	Файл = Новый Файл(ПолныйПутьКФайлуОтчета());
	ВремяИзменения = Дата("00010101");
	Если Файл.Существует() Тогда
		ВремяИзменения = Файл.ПолучитьВремяИзменения();
	КонецЕсли;

	Возврат ВремяИзменения;
	
КонецФункции

Функция ПутьКСводномуОтчетуПоВнесеннымИзменениям()

	Возврат "C:\RepositoryReport.txt";

КонецФункции

СформироватьОтчетыПоКонфигурациям();